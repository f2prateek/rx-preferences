buildscript {
    ext {
        minSdkVersion = 9
        targetSdkVersion = 28
        compileSdkVersion = 28
        buildToolsVersion = '28.0.3'
        javaVersion = JavaVersion.VERSION_1_7
        java8Version = JavaVersion.VERSION_1_8
        butterknifeVersion = '10.1.0'
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "com.jakewharton:butterknife-gradle-plugin:${butterknifeVersion}"
    }
}

plugins {
    id("net.ltgt.errorprone") version "0.7.1"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Werror']
    }

    group = GROUP
    version = VERSION_NAME
}

ext.deps = [
        annotations        : 'androidx.annotation:annotation:1.0.2',
        rxjava3            : 'io.reactivex.rxjava3:rxjava:3.0.4',
        rxandroid          : 'io.reactivex.rxjava3:rxandroid:3.0.0',
        rxbinding          : 'com.jakewharton.rxbinding4:rxbinding-core:4.0.0',
        butterknife        : "com.jakewharton:butterknife:${ext.butterknifeVersion}",
        butterknifeCompiler: "com.jakewharton:butterknife-compiler:${ext.butterknifeVersion}",
        junit              : 'junit:junit:4.12',
        assertj            : 'org.assertj:assertj-core:3.12.2',
        robolectric        : 'org.robolectric:robolectric:4.2.1',
        testcore           : 'androidx.test:core:1.0.0'
]
