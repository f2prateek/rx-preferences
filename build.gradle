buildscript {
    ext {
        buildToolsVersion = '28.0.3'
        butterknifeVersion = '10.1.0'
        compileSdkVersion = 28
        java8Version = JavaVersion.VERSION_1_8
        javaVersion = JavaVersion.VERSION_1_7
        kotlinVersion = '1.3.21'
        minSdkVersion = 9
        targetSdkVersion = 28
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "com.jakewharton:butterknife-gradle-plugin:${butterknifeVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id("net.ltgt.errorprone") version "0.7.1"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Werror']
    }

    group = GROUP
    version = VERSION_NAME
}

ext.deps = [
        annotations        : 'androidx.annotation:annotation:1.0.2',
        assertj            : 'org.assertj:assertj-core:3.12.2',
        butterknife        : "com.jakewharton:butterknife:${ext.butterknifeVersion}",
        butterknifeCompiler: "com.jakewharton:butterknife-compiler:${ext.butterknifeVersion}",
        junit              : 'junit:junit:4.12',
        kotlinStdlib       : "org.jetbrains.kotlin:kotlin-stdlib:${ext.kotlinVersion}",
        kotlinReflect      : "org.jetbrains.kotlin:kotlin-reflect:${ext.kotlinVersion}",
        robolectric        : 'org.robolectric:robolectric:4.2.1',
        rxandroid          : 'io.reactivex.rxjava2:rxandroid:2.1.1',
        rxbinding          : 'com.jakewharton.rxbinding:rxbinding:1.0.1',
        rxjava2            : 'io.reactivex.rxjava2:rxjava:2.2.7',
        rxjava2interop     : 'com.github.akarnokd:rxjava2-interop:0.13.5',
        testcore           : 'androidx.test:core:1.0.0'
]
